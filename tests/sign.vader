Given foobar:
  first line
  2nd line
  3rd line

Before:
  let buffer = bufnr('%')
  call comrade#buffer#Register(buffer, 42, v:false)
  function! CollectSigns()
      redir => l:output
         silent exec 'sign place'
      redir END

      let l:actual_sign_list = []

      for l:line in split(l:output, "\n")
        let l:match = matchlist(l:line, '\v^.*\=(\d+).*\=(\d+).*\=(CDE[a-zA-Z]+Sign)')

        if len(l:match) > 0
          call add(l:actual_sign_list, [l:match[1], l:match[2], l:match[3]])
        endif
      endfor

      return l:actual_sign_list
  endfunction

After:
  delfunction CollectSigns

Execute("Add and remove signs"):
  let buffer = bufnr('%')

  let insights = {
    \ 0 : [{'id' : 11, 's_line' : 0, 'severity' : 400}],
    \ 2 : [{'id' : 3, 's_line' : 2, 'severity' : 300}]
  \}
  call comrade#bvar#set(buffer, 'insight_map', insights)
  call comrade#sign#SetSigns(buffer)
  AssertEqual [['1', '11', 'CDEErrorSign'], ['3', '3', 'CDEWarningSign']], CollectSigns()

  let insights = {
    \ 1 : [{'id' : 22, 's_line' : 1, 'severity' : 400}],
    \ 2 : [{'id' : 3, 's_line' : 2, 'severity' : 300}]
  \}
  call comrade#bvar#set(buffer, 'insight_map', insights)
  call comrade#sign#SetSigns(buffer)
  AssertEqual [['2', '22', 'CDEErrorSign'], ['3', '3', 'CDEWarningSign']], CollectSigns()

  let insights = {
    \ 1 : [{'id' : 22, 's_line' : 1, 'severity' : 400}],
    \ 2 : [{'id' : 33, 's_line' : 2, 'severity' : 400}]
  \}
  call comrade#bvar#set(buffer, 'insight_map', insights)
  call comrade#sign#SetSigns(buffer)
  AssertEqual [['2', '22', 'CDEErrorSign'], ['3', '33', 'CDEErrorSign']], CollectSigns()

  let insights = {}
  call comrade#bvar#set(buffer, 'insight_map', insights)
  call comrade#sign#SetSigns(buffer)
  AssertEqual [], CollectSigns()

Execute("Signs should be cleard after buf unregistered"):
  let buffer = bufnr('%')

  let insights = {
    \ 0 : [{'id' : 11, 's_line' : 0, 'e_line' : 1, 's_col' : 0, 'e_col' : 1, 'severity' : 400}],
    \ 2 : [{'id' : 3, 's_line' : 2, 'e_line' : 2, 's_col' : 0, 'e_col' : 1, 'severity' : 300}],
  \}
  call comrade#SetInsights(42, buffer, insights)
  call comrade#sign#SetSigns(buffer)
  AssertEqual [['1', '11', 'CDEErrorSign'], ['3', '3', 'CDEWarningSign']], CollectSigns()

  call comrade#buffer#Unregister(buffer)
  Assert empty(CollectSigns())


Execute("Clear signs"):
  let buffer = bufnr('%')

  let insights = {
    \ 0 : [{'id' : 11, 's_line' : 0, 'e_line' : 1, 's_col' : 0, 'e_col' : 1, 'severity' : 400}],
    \ 2 : [{'id' : 3, 's_line' : 2, 'e_line' : 2, 's_col' : 0, 'e_col' : 1, 'severity' : 300}],
  \}
  call comrade#SetInsights(42, buffer, insights)
  call comrade#sign#SetSigns(buffer)
  AssertEqual [['1', '11', 'CDEErrorSign'], ['3', '3', 'CDEWarningSign']], CollectSigns()
  call comrade#sign#Clear(buffer)
  Assert empty(CollectSigns())
